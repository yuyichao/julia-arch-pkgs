pkgname=julia-zlib_jll-git
pkgver=1.2.11+9.0.1.gf71d146
pkgrel=2
pkgdesc="Zlib_jll.jl"
url="https://github.com/JuliaBinaryWrappers/Zlib_jll.jl"
arch=('any')
license=('MIT')
makedepends=(git julia-pkg-scripts)
depends=(julia zlib)
provides=('julia-zlib_jll')
source=(git://github.com/JuliaBinaryWrappers/Zlib_jll.jl
        jll.toml)
md5sums=('SKIP'
         'bd55890a5ff161a6514ed2ebc14ed69f')

pkgver() {
  cd Zlib_jll.jl

  git describe --tags | sed -e 's/^[^0-9]*//' -e 's/-/.0./' -e 's/-/./g'
}

build() {
  cd Zlib_jll.jl

  julia /usr/lib/julia/julia-gen-jll.jl Zlib_jll ../jll.toml .
}

package() {
  cd Zlib_jll.jl

  . /usr/lib/julia/julia-install-pkg.sh Zlib_jll "${pkgdir}" "${pkgname}"
}
